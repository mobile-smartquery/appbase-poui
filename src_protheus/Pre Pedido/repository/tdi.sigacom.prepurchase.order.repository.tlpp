
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include 'protheus.ch'
#INCLUDE "FWMVCDEF.CH"
#INCLUDE "TOPCONN.CH"
#INCLUDE "TRYEXCEPTION.CH"
#Define CRLF		Chr(13)+Chr(10)

namespace tdi.sigacom.prepurchase.order.repository

/*/{Protheus.doc} TdiCOMPrePurchaseOrderRepository()
    Classe responsavel pela execucao de consultas no banco de dados
    @type Class
    @author Cassio Menabue Lima
    @since 24/02/2023
    @version 1.0
/*/
Class TdiCOMPrePurchaseOrderRepository FROM FWAdapterBaseV2
	Public Data jResponse as Json
	Public method New(cVerbo,lList)
	Public Method GetOrders(nPage, nPageSize, nPage, nPageSize, cOrder)
	Public method SetOrder(jBody   )
	Public method CancelOrder(jBody   )
	Public Method Destroy()
	Public Method TestUnit(nOper,cFil,cPedido)
	Public Method GetEmaiLUser(cCod)

	Private Method GetEmailsApprovals(jScr)
	Private Method AddMainMapFields( oSelf )
	Private Method GetMainQuery(cOrder)
	Private Method GetPrePurchaseOrder(cOrder)
	Private Method GetItems(cOrder)
	Private Method GetApportionment(cOrder)
	Private Method GetApprovals(cFil, cNum)
	Private Method saveData(jBody  )
	Private Method GetDataAux(cTab, cWhere)
	Private Method SetCancelOrder()

EndClass

/*/{Protheus.doc} New()
    Method responsavel por instanciar a classe e iniciar variaveis
    @type Method
    @author Cassio Menabue Lima
    @since 20/09/2024
    @version 1.0
/*/
Method New(cVerbo as Character, lList as Logical) Class TdiCOMPrePurchaseOrderRepository
	Default cVerbo    := 'GET'
	Default lList     := .T.
	::jResponse:=JsonObject():New()
	_Super:New(cVerbo, lList)

Return Self
/*/{Protheus.doc} GetOrders()
    Realiza a consulta no banco para retornar o GET da requisicao
    @type  Method
    @author Cassio Menabue Lima
    @since 20/09/2024
    @version 1.0
/*/
Method GetOrders(nPage, nPageSize, cOrder) class TdiCOMPrePurchaseOrderRepository
	Local cQuery        as Character
	Local cWhere        as Character
	Local jsonResponse  as Object
	Local nI 			:= 0
	Local cPedido       := ""
	Default cFil        := ""
	Default nPage       := 1
	Default nPageSize   := 20
	Default cOrder      := ""


	cWhere := " PAX.D_E_L_E_T_ =' ' AND PAX_FILIAL <>' '"
	If  !Empty(cOrder)
		cWhere += "  AND PAX_FILIAL||PAX_NUM = '"+cOrder+"'  "
	EndIF

	cQuery :=  ::GetMainQuery()

	::AddMainMapFields( self )
	::setPage(nPage)
	::setPageSize(nPagesize)
	::setQuery(cQuery)
	::setWhere(cWhere)

	If ::Execute()
		::FillGetResponse()
		jsonResponse := ::GetJsonResponse()
		oJs   := JsonObject():New()
		oJs:FromJSON( jsonResponse )
		For nI:= 1 to Len(oJs['items'])
			jItem:=oJs['items'][nI]
			cFil    := jItem['pax_filial']
			cOrder  := jItem['pax_num']
			cPedido := jItem['pax_pedido']

			oJs['items'][nI]['PAU']:=::GetItems(cOrder)
			oJs['items'][nI]['PAY']:=::GetApportionment(cOrder)
			jScr:= ::GetApprovals(cFil, cPedido)
			oJs['items'][nI]['SCR']:=::GetEmailsApprovals(jScr)
		Next nI
		jsonResponse:=oJs:toJson()
		FwFreeObj(oJs)
	EndIf

Return jsonResponse

/*/{Protheus.doc} AddMainMapFields()
    Methodo Responsavel por Setar os campos da querie
    @type  Method
    @author Cassio Menabue Lima
    @since 20/09/2024
    @version 1.0
/*/
Method AddMainMapFields( oSelf ) class TdiCOMPrePurchaseOrderRepository
	Local aFields	 := FWSX3Util():GetAllFields( "PAX" ,.F. )
	Local nA 	     := 0
	For nA := 1 to LEN( aFields )
		cCampo := Alltrim(FWSX3Util():GetFieldStruct( aFields[NA] )[1])
		cTipo  := FWSX3Util():GetFieldStruct( aFields[NA] )[2]
		nTaman := FWSX3Util():GetFieldStruct( aFields[NA] )[3]
		nDecim := FWSX3Util():GetFieldStruct( aFields[NA] )[4]
		oSelf:AddMapFields(cCampo, cCampo  , .T., .T., { cCampo	, cTipo, nTaman, nDecim } )
	Next

Return
/*/{Protheus.doc} GetMainQuery()
    Methodo Responsavel por Gerar a querie Principal
    @type  Method
    @author Cassio Menabue Lima
    @since 20/09/2024
    @version 1.0
/*/
Method GetMainQuery() Class TdiCOMPrePurchaseOrderRepository
	Local cQuery AS CHARACTER

	//Obtem a ordem informada na requisição, a query exterior SEMPRE deve ter o id #QueryFields# ao invés dos campos fixos
	//necessáriamente não precisa ser uma subquery, desde que não contenha agregadores no retorno ( SUM, MAX... )
	//o id #QueryWhere# é onde será inserido o clausula Where informado no método SetWhere()
	cQuery := " SELECT #QueryFields# "
	cQuery += " FROM   " + RetSQLName("PAX") + " PAX    "
	cQuery += " WHERE #QueryWhere#"
	cQuery += " "

Return cQuery



/*/{Protheus.doc} SetOrder()
    Methodo Responsavel por Gravar os pre pedidos
    @type  Method
    @author Cassio Menabue Lima
    @since 20/09/2024
    @version 1.0
/*/

Method SetOrder(jBody as Json) class TdiCOMPrePurchaseOrderRepository

return ::saveData(jBody)


/*/{Protheus.doc} SetOrder()
    Methodo Responsavel por Gravar os pre pedidos
    @type  Method
    @author Cassio Menabue Lima
    @since 20/09/2024
    @version 1.0
/*/

Method CancelOrder(jBody as Json) class TdiCOMPrePurchaseOrderRepository


return ::saveData(jBody)

/*/{Protheus.doc} saveData()
    Methodo Responsavel por Gravar Pre Pedidos
    @type  Method
    @author Cassio Menabue Lima
    @since 20/09/2024
    @version 1.0
/*/
Method saveData(jBody as Json ) class TdiCOMPrePurchaseOrderRepository

	Local nI
	Local aArea         := GetArea()
	Local aItens        := {}
	Local oError
	Local cError		:=""
	Local lValid		:=.F.
	Local nK			:= 0
	Local nTotPAX       := 0
	Local nTotPAU       := 0
	Local nTotPAY       := 0

	Local aPAXItem      := {}

	Local aPAX			:= {}
	Local aPAU			:= {}
	Local aPAY			:= {}
	Local nOpcR         := 0

	Default cEmp 		:= cEmpAnt
	Default cFil 		:= cFilAnt
	Private lMsErroAuto := .F.
	Private lMsHelpAuto := .T.

	if !jBody:HasProperty("PAX")
		::jResponse["id"]:=-1
		::jResponse["message"]:="Nao foi informado nenhum pre pedido"
		return ::jResponse
	EndIF

	nTotPAX:=Len(jBody["PAX"])
	If nTotPAX == 0
		::jResponse["id"]:=-1
		::jResponse["message"]:="Nao foi informado nenhum pre pedido"
		return ::jResponse
	EndIF

	if jBody:HasProperty("PAU")
		nTotPAU:=Len(jBody["PAU"])
	EndIF

	if jBody:HasProperty("PAY")
		nTotPAY:=Len(jBody["PAY"])
	EndIF
	if !jBody:HasProperty("operation")
		::jResponse["id"]:='-1'
		::jResponse["message"]:="nao foi informado  a tag operation no json"
		return ::jResponse
	EndIF
	nOpcR:=jBody["operation"]
	cOrder:=""
	if jBody:HasProperty("order")
		cOrder:= jBody["order"]
	EndIF
	::jResponse["log"]:=jBody:toJson()

	TRYEXCEPTION
	For nI:= 1 to nTotPAX
		aPAXItem := {}
		cMsg := " "
		cCampo :=  Alltrim(FWSX3Util():GetFieldStruct( Upper(jBody["PAX"][nI]["field"]))[1])
		cTipo  := FWSX3Util():GetFieldStruct( Upper(jBody["PAX"][nI]["field"]))[2]
		//Valida se o Campo data para converter de string para data
		if( cTipo =="D")
			jBody["PAX"][nI]["value"]:= STOD(jBody["PAX"][nI]["value"])
		EndIF
		if(Upper(jBody["PAX"][nI]["field"]) =="PAX_FILIAL")
			cFil:= jBody["PAX"][nI]["value"]
		EndIF

		aAdd(aPAXItem, {Upper(jBody["PAX"][nI]["field"]), iif(cTipo == "D" .or. cTipo == "N",jBody["PAX"][nI]["value"], DecodeUtf8(jBody["PAX"][nI]["value"], "cp1252" )), nil} )

		aAdd(aPAX, aclone(aPAXItem) )

	Next nI

	For nI:= 1 to nTotPAU
		if(!jBody["PAU"][nI]:HasProperty("item"))
			Loop
		EndIF
		aPauItem:=jBody["PAU"][nI]["item"]
		If Len(aPauItem)==0
			Loop
		EndIF

		aPAUIt := {}
		For nK:=1 to len(aPauItem)
			cTipo  := FWSX3Util():GetFieldStruct( Upper(aPauItem[nK]["field"]))[2]
			//Valida se o Campo data para converter de string para data
			if( cTipo =="D")
				aPauItem[nK]["value"]:= STOD(aPauItem[nK]["value"])
			Elseif ( cTipo =="C") .Or. ( cTipo =="M")
				aPauItem[nK]["value"] := DecodeUtf8(aPauItem[nK]["value"], "cp1252" )
			EndIF
			aAdd(aPAUIt, {Upper(aPauItem[nK]["field"]), aPauItem[nK]["value"], nil} )
		Next nK
		aAdd(aPAU, aclone(aPAUIt) )
	Next nI

	For nI:= 1 to nTotPAY
		if(!jBody["PAY"][nI]:HasProperty("item"))
			Loop
		EndIF
		aPayItem:=jBody["PAY"][nI]["item"]
		If Len(aPayItem)==0
			Loop
		EndIF
		aPAYIt := {}
		For nK:=1 to len(aPayItem)
			cTipo  := FWSX3Util():GetFieldStruct( Upper(aPayItem[nK]["field"]))[2]
			//Valida se o Campo data para converter de string para data
			if( cTipo =="D")
				aPayItem[nK]["value"]:= STOD(aPayItem[nK]["value"])
			EndIF
			aAdd(aPAYIt, {Upper(aPayItem[nK]["field"]),aPayItem[nK]["value"], nil} )
		Next nK
		aAdd(aPAY, aclone(aPAYIt) )
	Next nI

	lValid		:= .F.
	lMsErroAuto := .F.
	lMsHelpAuto := .T.
	lValid:=.T.
	aItens :={}

	If nOpcR <> 5
		nOpcR:=3
	EndIf
	If  Empty(cOrder) .And. nOpcR != 3
		::jResponse["id"]:='-1'
		::jResponse["message"]:="Nao foi informado nenhum codigo pre pedido"
		return ::jResponse
	EndIF
	DbSelectArea("PAX")
	DbSetOrder(1)
	If( !Empty(Alltrim(cOrder)) .And. PAX->(Dbseek(cOrder)))
		If nOpcR <> 5
			nOpcR:=4
		Else // Cancelamento
			::SetCancelOrder()
			return ::jResponse
		EndIf
	Else
		If 	nOpcR != 3
			::jResponse["id"]:='-1'
			::jResponse["message"]:="Nao foi possivel localizar o Pre-pedido de compras n°("+cOrder+")"
			return ::jResponse
		EndIF
	EndIF
	cFilAnt := cFil
	FWSM0Util():setSM0PositionBycFilAnt()

	if lValid
		aRotAuto := {}
		aadd(aRotAuto,aclone(aPAX))
		aadd(aRotAuto,aclone(aPAU))
		aadd(aRotAuto,aclone(aPAY))

		MSExecAuto({|x, y| U_TCOMA012(x,y)}, aRotAuto, nOpcR)

		If !lMsErroAuto
			::jResponse['id']:=PAX->PAX_FILIAL+"_"+PAX->PAX_NUM
			::jResponse['message']:="Operacao do pre pedido finalizada com sucesso"
		Else // EM ESTADO DE JOB
			cTime:= dTos(Date()) + strtran(IncTime(time() ,3,0,0 ),":")
			cError := MostraErro("/system/prepedido/", "PREPURCHASEORDER_"+cTime+".log")
			::jResponse['id']:='-1'
			::jResponse['message']:=cError
		EndIf
	EndIF


	CATCHEXCEPTION USING oError
	cMsgFault:=""
	IF ( ValType( oError ) == "O" )
		cMsgFault := IF( !Empty( oError:Description )	, oError:Description	, "" )
		cMsgFault += IF( !Empty( oError:ErrorStack )	, oError:ErrorStack 	, "" )
	Else
		cMsgFault:="Erro Desconhecido linha 359 do programa tdi.sigacom.prepurchase.order.repository.tlpp"
	EndIF
	::jResponse['id']:='-1'
	::jResponse['message']:=cMsgFault
	IF InTransact()
		DisarmTransaction()
	EndIF
	ENDEXCEPTION

	RestArea(aArea)


return ::jResponse



/*/{Protheus.doc} Destroy()
    Method responsavel por limpar o objeto  
    @type Method
    @author Cassio Menabue Lima
    @since20/09/2024
    @version 1.0
/*/
Method Destroy() class TdiCOMPrePurchaseOrderRepository

return
/*/{Protheus.doc} GetPrePurchaseOrder()
   por retornar um JSON com os dados da  do Pre Pedido de compra e seus rateios e itens
    @type Method
    @author Cassio Menabue Lima
    @since20/09/2024
    @version 1.0
/*/
Method GetPrePurchaseOrder(cOrder)  class TdiCOMPrePurchaseOrderRepository

Return jPurchase
/*/{Protheus.doc} GetItems()
    Method responsavel por retornar um JSON com os dados da PAU de items do Pre Pedido de compra
    @type Method
    @author Cassio Menabue Lima
    @since20/09/2024
    @version 1.0
/*/
Method GetItems(cOrder)  class TdiCOMPrePurchaseOrderRepository
	Local cWhere := " PAU_NUM ='"+cOrder+"' AND PAU.D_E_L_E_T_ =' '"
return ::GetDataAux("PAU",cWhere)

/*/{Protheus.doc} GetApprovals(cFil,cNum)
    Method responsavel por retornar um JSON com os dados da SCR de items do Pre Pedido de compra
    @type Method
    @author Matheus Gratão D'Ávila
    @since 25/09/2024
    @version 1.0
/*/
Method GetApprovals(cFil, cNum)  class TdiCOMPrePurchaseOrderRepository
	Local cTipo  :=  Formatin(SuperGetMv("TI_TCOM12T",,"PC/IP"),"/")
	Local cWhere := " CR_FILIAL ='" + cFil + "' AND CR_NUM ='" + cNum + "' AND CR_TIPO IN " + cTipo + " AND SCR.D_E_L_E_T_ =' '"
return ::GetDataAux("SCR", cWhere)

/*/{Protheus.doc} GetApportionment()
    Method responsavel por retornar um JSON com os dados da PAY do Pre Pedido de compra
    @type Method
    @author Cassio Menabue Lima
    @since 20/09/2024
    @version 1.0
/*/

Method GetApportionment(cOrder)  class TdiCOMPrePurchaseOrderRepository
	Local cWhere := " PAY_NUM ='"+cOrder+"' AND PAY.D_E_L_E_T_ =' '"
return ::GetDataAux("PAY",cWhere)
/*/{Protheus.doc} GetDataAux()
    Method responsavel por retornar um JSON com os dados da tabela selecionada do Pre Pedido de compra
    @type Method
    @author Cassio Menabue Lima
    @since 20/09/2024
    @version 1.0
/*/

Method GetDataAux(cTab,cWhere) class TdiCOMPrePurchaseOrderRepository
	Local aFields	 := FWSX3Util():GetAllFields( cTab,.F. )
	Local nA 	     := 0
	Local cAliGer  := GetNextAlias()
	Local aDados     :={}
	Local oDados     :=JsonObject():New()


	If Select(cAliGer) > 0
		(cAliGer)->(dbCloseArea())
	EndIf
	cQry:="SELECT *  "
	cQry += " FROM   " + RetSQLName(cTab) + " "+cTab
	cQry += " WHERE "+cWhere


	cQry := ChangeQuery(cQry)

	dbUseArea(.T.,"TOPCONN", TCGenQry(,,cQry), cAliGer, .F., .T.)
	DbSelectArea(cTab)
	dbSetOrder(1)
	(cAliGer)->(DbGoTop())

	While (cAliGer)->(!Eof())
		oDados     :=JsonObject():New()
		For nA := 1 to LEN( aFields )
			cCampo :=  Alltrim(FWSX3Util():GetFieldStruct( aFields[NA] )[1])
			cTipo  := FWSX3Util():GetFieldStruct( aFields[NA] )[2]
			if (cTipo =="M")
				(cTab)->(dbGoTo((cAliGer)->R_E_C_N_O_))
				oDados[cCampo]:=(cTab)->&(cCampo)
			Else
				oDados[cCampo]:=(cAliGer)->&(cCampo)
			EndIF
		Next
		Aadd(aDados,oDados)
		//
		(cAliGer)->(DbSkip())
	enddo
	(cAliGer)->(dbCloseArea())
	aRet:=aClone(aDados)
	aDados:={}
	FwFreeObj(oDados)
Return aRet
/*/{Protheus.doc} SetCancelOrder()
    Method responsavel por cancelar um Pre pedido
    @type Method
    @author Cassio Menabue Lima
    @since 20/09/2024
    @version 1.0
/*/

Method	SetCancelOrder()  class TdiCOMPrePurchaseOrderRepository

	if(Alltrim(PAX->PAX_STATUS) =="2")
		::jResponse['id']    :="-1"
		::jResponse['message']:="Pre-pedido de compra já encontra-se cancelado"
		Return
	EndIF

	if(!Empty(Alltrim(PAX->PAX_PEDIDO)))
		::jResponse['id']     :='-1'
		::jResponse['message']:="Nao é possivel cancelar este pre-pedido de compra, Motivo: Existe um pedido de compra n°("+PAX->PAX_PEDIDO+") Gerado para esse Pre pedido "
		Return
	EndIF
	Reclock("PAX",.F.)
	PAX->PAX_STATUS :="2"//Cancelado
	PAX->(MsUnlock())
	::jResponse['id']     :=PAX->PAX_FILIAL+"_"+PAX->PAX_NUM
	::jResponse['message']:="Pre-pedido de compra cancelado com sucesso"
Return

/*/{Protheus.doc} GetEmaiLUser()
    Method responsavel por retornar o email do codigo do usuario do protheus informado no parametro cUser
    @type Method
    @author Cassio Menabue Lima
    @since 01/10/2024
    @version 1.0
/*/
Method GetEmaiLUser(c_User)  class TdiCOMPrePurchaseOrderRepository
Return AllTrim(UsrRetMail(c_User))
/*/{Protheus.doc} GetEmailsApprovals()
    Method responsavel por incluir as propriedades emails dos campos de usuarios contidos na SCR
    @type Method
    @author Cassio Menabue Lima
    @since  01/10/2024
    @version 1.0
/*/
Method GetEmailsApprovals(jScr) class TdiCOMPrePurchaseOrderRepository
	Local nScr:=0
	For nScr:= 1 to Len(jScr)
		jScr[nScr]['CR_EMLUSER']	:=::GetEmaiLUser(jScr[nScr]['CR_USER'])
		jScr[nScr]['CR_EMLAPRO']	:=::GetEmaiLUser(jScr[nScr]['CR_APROV'])
		jScr[nScr]['CR_EMLUSRL']	:=::GetEmaiLUser(jScr[nScr]['CR_USERLIB'])
		jScr[nScr]['CR_EMLLIBA']	:=::GetEmaiLUser(jScr[nScr]['CR_LIBAPRO'])
	Next nScr
Return jScr
/*/{Protheus.doc} TestUnit()
    Method responsavel pelos testes unitarios
    @type Method
    @author Cassio Menabue Lima
    @since 20/09/2024
    @version 1.0
	oRepository :=  tdi.sigacom.prepurchase.order.repository.TdiCOMPrePurchaseOrderRepository():new()
	oRepository:TestUnit(4,"00001000100","000025"):toJson()
/*/

Method TestUnit(nOper,cFil,cPedido) class TdiCOMPrePurchaseOrderRepository
	Local oDados     :=JsonObject():New()
	Local cOrder:=cFil+cPedido
	if nOper <> 4
	 BeginContent VAR cJson
{
	"operation": %Exp:nOper%,
	"order":	'"'%Exp:cFil%%Exp:cPedido%'"',
	"PAX": [
		{
			"field": "pax_filial",
			"value": "%Exp:cOrder%"
		},
		{
			"field": "pax_emissa",
			"value": "20241003"
		},
		{
			"field": "pax_fornec",
			"value": "009044"
		},
		{
			"field": "pax_loja",
			"value": "00"
		},
		{
			"field": "pax_cgcfor",
			"value": "36923086809"
		},
		{
			"field": "pax_tpcd",
			"value": ""
		},
		{
			"field": "pax_scflui",
			"value": "4598460"
		},
		{
			"field": "pax_linkfl",
			"value": "https://totvsti183126.fluig.cloudtotvs.com.br:10751/portal/p/10097/pageworkflowview?app_ecm_workflowview_detailsProcessInstanceID=4598460"
		},
		{
			"field": "pax_emlusr",
			"value": "ecm@byyou.com.br"
		},
		{
			"field": "pax_emlsol",
			"value": "ecm@byyou.com.br"
		},
		{
			"field": "pax_contra",
			"value": ""
		},
		{
			"field": "pax_solici",
			"value": ""
		},
		{
			"field": "pax_filent",
			"value":"%Exp:cFil%"
		},
		{
			"field": "pax_total",
			"value": 150000
		},
		{
			"field": "pax_cond",
			"value": ""
		},
		{
			"field": "pax_moeda",
			"value": 1
		},
		{
			"field": "pax_txmoed",
			"value": 0
		},
		{
			"field": "pax_diferi",
			"value": "2"
		},
		{
			"field": "pax_dtdif1",
			"value": ""
		},
		{
			"field": "pax_dtdif2",
			"value": ""
		},
		{
			"field": "pax_banco",
			"value": ""
		},
		{
			"field": "pax_agenci",
			"value": ""
		},
		{
			"field": "pax_numcon",
			"value": ""
		},
		{
			"field": "pax_fopgmt",
			"value": "5"
		},
		{
			"field": "pax_justif",
			"value": "teste"
		},
		{
			"field": "pax_pginfo",
			"value": "teste"
		},
		{
			"field": "pax_pgtant",
			"value": "2"
		},
		{
			"field": "pax_status",
			"value": "0"
		}
	],
	"PAU": [],
	"PAY": [
		{
			"item": [
				{
					"field": "pay_cc",
					"value": "101100100"
				},
				{
					"field": "pay_itemct",
					"value": "0100     "
				},
				{
					"field": "pay_clvl",
					"value": "C0103    "
				},
				{
					"field": "pay_valor",
					"value": 100000
				}
			]
		},
		{
			"item": [
				{
					"field": "pay_cc",
					"value": "101100101"
				},
				{
					"field": "pay_itemct",
					"value": "0100     "
				},
				{
					"field": "pay_clvl",
					"value": "C0104    "
				},
				{
					"field": "pay_valor",
					"value": 50000
				}
			]
		}
	]
}
	EndContent
	Else
	 BeginContent VAR cJson
		{ "operation": 4,
		  "order": "%Exp:cOrder%",
		    "PAX": [
		        {
		            "field": "pax_txmoed",
		            "value": 15.50
		        }
		    ]
		}
	 EndContent
	EndIF
	oDados:FromJSON(cJson)
	if(nOper==3)//Testa inclusao
		oRet:=::SetOrder(oDados   )
	EndIF
	if(nOper==4)//Testa Alteracao
		oRet:=::SetOrder(oDados   )
	EndIF
	if(nOper==5)//Testa Exclusao
		oRet:=::CancelOrder(oDados   )

	EndIF
Return  oRet

