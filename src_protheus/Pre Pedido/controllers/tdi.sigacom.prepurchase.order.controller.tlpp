#include "tlpp-core.th"
#include "tlpp-rest.th"
#Include "TOTVS.CH"
#Include "Protheus.ch"
#INCLUDE "TRYEXCEPTION.CH"
#INCLUDE "PARMTYPE.CH"
#INCLUDE "TOPCONN.CH"
namespace tdi.sigacom.prepurchase.order.controller
using namespace tdi.sigacom.prepurchase.order.service

/*/{Protheus.doc} TdiCOMPrePurchaseOrder()
    Classe responsavel por receber as requisicoes de Grupo de Produto
    @type Class
    @author Cassio Menabue Lima
    @since 20/09/2024
    @version 1.0
/*/
Class TdiCOMPrePurchaseOrderControllers from LongNameClass

	public data oServices
	public data  jBody               as object
	public data  jParams             as object
	public data  jPathParams         as object
	public data  cPage               as Character
	public data  cPageSize           as Character
	public data  cFilter             as Character

	public method New()


	@Get("/api/tdi/com/v1/prepurchaseorder/orders")
	public method orders()
	@Get("/api/tdi/com/v1/prepurchaseorder/order/:order")
	public method order()
	@Post("/api/tdi/com/v1/prepurchaseorder/order")
	public method PostPrePurchaseOrder()
	@Put("/api/tdi/com/v1/prepurchaseorder/change/:order")
	public method PutPrePurchaseOrder()
	@Delete("/api/tdi/com/v1/prepurchaseorder/cancel/:order")
	public method PostCancelPrePurchaseOrder()



EndClass

/*/{Protheus.doc} New()
    Metodo responsavel por instanciar a classe e iniciar variaveis
    @type Method
    @author Cassio Menabue Lima
    @since 20/09/2024
    @version 1.0
/*/
Method New() class TdiCOMPrePurchaseOrderControllers
	::oServices :=  tdi.sigacom.prepurchase.order.service.TdiCOMPrePurchaseOrderServices():new()
	::jParams   := JsonObject():New()
	::jPathParams:= JsonObject():New()
Return Self

/*/{Protheus.doc} orders()
    Recebe os dados da requisicao de GET para Efetua a consulta de dados de todos pre pedidos
    @type  Method
    @author Cassio Menabue Lima
    @since 20/09/2024
    @version 1.0
    //Exemplo POSTMAN - GET:
    http://172.24.35.104:7038/app-root/api/tdi/gct/v1/cancellationapproval/items?page=1&pageSize=10&zrm_status=1&filter=codigo eq '0001' and (contains(filial, 'D MG 01') or contains(filial, 'M PR 02 '))
/*/
Method orders() Class TdiCOMPrePurchaseOrderControllers
	Local jBodyResponse as Object
	Local nPage         as Numeric
	Local nPageSize     as Numeric
	oRest:setKeyHeaderResponse("Content-Type", "application/json")
	::jParams        := oRest:getQueryRequest()
	jBodyResponse    := JsonObject():New()

	::cPage             := ::jParams['page']
	::cPageSize         := ::jParams['pageSize']


	if empty(::cPage)
		nPage := 1
	else
		nPage := VAL(::cPage)
	endif
	if empty(::cPageSize)
		nPageSize := 10
	else
		nPageSize := Val(::cPageSize)
	endif

	oRest:setResponse( ::oServices:GetOrders(nPage, nPageSize))
Return
/*/{Protheus.doc} order()
    Recebe os dados da requisicao de GET para Efetua a consulta do pre pedido selecionado
    @type  Method
    @author Cassio Menabue Lima
    @since 20/09/2024
    @version 1.0
    //Exemplo POSTMAN - GET:
    http://localhost:1243/restapi/tdi/contratos/v1/cronograma?page=1&pageSize=10&filter=codigo eq '0001' and (contains(filial, 'D MG 01') or contains(filial, 'M PR 02 '))
/*/
Method order() Class TdiCOMPrePurchaseOrderControllers
	Local jBodyResponse as Object
	Local nPage         as Numeric
	Local nPageSize     as Numeric
	Local cOrder  		as character


	oRest:setKeyHeaderResponse("Content-Type", "application/json")
	::jPathParams := oRest:getPathParamsRequest()
	::jParams     := oRest:getQueryRequest()
	jBodyResponse := JsonObject():New()
	cOrder 		  := ::jPathParams['order']
	::cPage       := ::jParams['page']
	::cPageSize   := ::jParams['pageSize']

	if empty(::cPage)
		nPage := 1
	else
		nPage := VAL(::cPage)
	endif
	if empty(::cPageSize)
		nPageSize := 10
	else
		nPageSize := Val(::cPageSize)
	endif
	cOrder:=::oServices:getParamValue(cOrder)
	oRest:setResponse( ::oServices:GetOrders(nPage, nPageSize,cOrder))
Return
/*/{Protheus.doc} PostPrePurchaseOrder()
    Recebe os dados da requisicao de POST para Efetua a gravação no contrato
    @type  Method
    @author Cassio Menabue Lima
    @since 20/09/2024
    @version 1.0
    //Exemplo POSTMAN - POST:
    http://localhost:1243/restapi/tdi/contratos/v1/cronograma?page=1&pageSize=10&filter=codigo eq '0001' and (contains(filial, 'D MG 01') or contains(filial, 'M PR 02 '))
/*/
Method PostPrePurchaseOrder() class TdiCOMPrePurchaseOrderControllers
	Local jBodyRequest  := JsonObject():New() as json
	Local jBodyResponse := JsonObject():New() as json
	Local cResponse                           as character

	oRest:setKeyHeaderResponse('Content-type','application/json')
	jBodyRequest:fromJson(oRest:getBodyRequest())
	jResp:=::oServices:setOrder(jBodyRequest )
	if (jResp['id'] != '-1')
		jBodyResponse := jResp
		cResponse := jBodyResponse:toJson()
		oRest:setResponse(cResponse)
	Else
		jBodyResponse := jResp
		cResponse := jBodyResponse:toJson()
		oRest:setStatusCode(400)
		oRest:setResponse(cResponse)
	Endif

	FreeObj(jBodyRequest)
	FreeObj(jBodyResponse)

Return
/*/{Protheus.doc} PutPrePurchaseOrder()
    Recebe os dados da requisicao de PUT para alteracao de pre pedido de compra
    @type  Method
    @author Cassio Menabue Lima
    @since 20/09/2024
    @version 1.0
    //Exemplo POSTMAN - POST:
    http://localhost:1243/restapi/tdi/contratos/v1/cronograma?page=1&pageSize=10&filter=codigo eq '0001' and (contains(filial, 'D MG 01') or contains(filial, 'M PR 02 '))
/*/
Method PutPrePurchaseOrder() class TdiCOMPrePurchaseOrderControllers
	Local jBodyRequest  := JsonObject():New() as json
	Local jBodyResponse := JsonObject():New() as json
	Local cResponse                           as character
	Local cOrder   							  as character
	oRest:setKeyHeaderResponse('Content-type','application/json')
	::jPathParams := oRest:getPathParamsRequest()
	::jParams     := oRest:getQueryRequest()
	cOrder		  := ::jPathParams['order']
	jBodyRequest:fromJson(oRest:getBodyRequest())
	jResp:=::oServices:setOrder(jBodyRequest )
	if (jResp['id'] != '-1')
		jBodyResponse := jResp
		cResponse := jBodyResponse:toJson()
		oRest:setResponse(cResponse)
	Else
		jBodyResponse := jResp
		cResponse := jBodyResponse:toJson()
		oRest:setStatusCode(400)
		oRest:setResponse(cResponse)
	Endif


	FreeObj(jBodyRequest)
	FreeObj(jBodyResponse)

Return

/*/{Protheus.doc} PostCancelPrePurchaseOrder()
    Recebe os dados da requisicao de POST para cancelar um pre pedido de compra
    @type  Method
    @author Cassio Menabue Lima
    @since 20/09/2024
    @version 1.0
    //Exemplo POSTMAN - POST:
    http://localhost:1243/restapi/tdi/contratos/v1/cronograma?page=1&pageSize=10&filter=codigo eq '0001' and (contains(filial, 'D MG 01') or contains(filial, 'M PR 02 '))
/*/
Method PostCancelPrePurchaseOrder() class TdiCOMPrePurchaseOrderControllers
	Local jBodyRequest  := JsonObject():New() as json
	Local jBodyResponse := JsonObject():New() as json
	Local cResponse                           as character
	Local cOrder  							  as character
	oRest:setKeyHeaderResponse('Content-type','application/json')
	::jPathParams := oRest:getPathParamsRequest()
	::jParams     := oRest:getQueryRequest()
	jBodyRequest:fromJson(oRest:getBodyRequest())
	cOrder		  := ::jPathParams['order']

	jResp:=::oServices:cancelOrder(jBodyRequest )
	if (jResp['id'] != '-1')
		jBodyResponse := jResp
		cResponse := jBodyResponse:toJson()
		oRest:setResponse(cResponse)
	Else
		jBodyResponse := jResp
		cResponse := jBodyResponse:toJson()
		oRest:setStatusCode(400)
		oRest:setResponse(cResponse)
	Endif

	FreeObj(jBodyRequest)
	FreeObj(jBodyResponse)

Return
